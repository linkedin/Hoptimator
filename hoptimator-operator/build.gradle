plugins {
  id 'java'
  id 'maven-publish'
  id 'idea'
}

dependencies {
  implementation project(':hoptimator-avro')
  implementation project(':hoptimator-planner')
  implementation project(':hoptimator-catalog') // <-- marked for deletion
  implementation project(':hoptimator-util')
  implementation project(':hoptimator-k8s')
  implementation project(':hoptimator-models')  // <-- marked for deletion

  implementation libs.calcite.core
  implementation libs.kubernetes.client
  implementation libs.kubernetes.extended.client
  implementation libs.slf4j.api
  implementation libs.commons.cli
  implementation libs.avro

  testImplementation(testFixtures(project(':hoptimator-k8s')))
  testImplementation(platform('org.junit:junit-bom:5.11.3'))
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly libs.slf4j.simple
}

test {
  useJUnitPlatform {
    excludeTags 'integration'
  }
  testLogging {
    events "passed", "skipped", "failed"
  }
}

publishing {
  repositories {
    maven {
      name 'GitHubPackages'
      url = 'https://maven.pkg.github.com/linkedin/Hoptimator'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name 'LinkedInJFrog'
      url 'https://linkedin.jfrog.io/artifactory/hoptimator'
      credentials {
        username = System.getenv('JFROG_USERNAME')
        password = System.getenv('JFROG_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = 'com.linkedin.hoptimator'
      artifactId = 'hoptimator-operator'
      version = System.getenv('VERSION')
      from components.java
      pom {
        name = 'LinkedIn Hoptimator'
        description = 'Multi-hop declarative data pipelines'
        url = 'https://github.com/linkedin/Hoptimator'
        licenses {
          license {
            name = 'BSD 2-Clause'
            url = 'https://raw.githubusercontent.com/linkedin/Hoptimator/main/LICENSE'
            }
          }
        scm {
          connection = 'scm:git:git://github.com:linkedin/Hoptimator.git'
          developerConnection = 'scm:git:ssh://github.com:linkedin/Hoptimator.git'
          url = 'https://github.com/linkedin/Hoptimator'
        }
      }
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}
