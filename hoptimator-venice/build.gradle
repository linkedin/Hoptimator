plugins {
  id 'java-library'
  id 'maven-publish'
}

dependencies {
  implementation project(':hoptimator-avro')
  implementation project(':hoptimator-util')
  implementation libs.calcite.core
  implementation(libs.venice) {
    // Venice pulls in snakeyaml v2.0 which has conflicting APIs with 1.x
    exclude group: 'org.yaml'
  }
  implementation libs.venice.client

  testImplementation libs.assertj

  testRuntimeOnly project(':hoptimator-k8s')
  testImplementation(testFixtures(project(':hoptimator-jdbc')))

  // These are included in case the respective Databases are installed
  testRuntimeOnly project(':hoptimator-demodb')
  testRuntimeOnly project(':hoptimator-mysql')
}

publishing {
  repositories {
    maven {
      name 'GitHubPackages'
      url = 'https://maven.pkg.github.com/linkedin/Hoptimator'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name 'LinkedInJFrog'
      url 'https://linkedin.jfrog.io/artifactory/hoptimator'
      credentials {
        username = System.getenv('JFROG_USERNAME')
        password = System.getenv('JFROG_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = 'com.linkedin.hoptimator'
      artifactId = 'hoptimator-venice'
      version = System.getenv('VERSION')
      from components.java
      pom {
        name = 'LinkedIn Hoptimator'
        description = 'Multi-hop declarative data pipelines'
        url = 'https://github.com/linkedin/Hoptimator'
        licenses {
          license {
            name = 'BSD 2-Clause'
            url = 'https://raw.githubusercontent.com/linkedin/Hoptimator/main/LICENSE'
          }
        }
        scm {
          connection = 'scm:git:git://github.com:linkedin/Hoptimator.git'
          developerConnection = 'scm:git:ssh://github.com:linkedin/Hoptimator.git'
          url = 'https://github.com/linkedin/Hoptimator'
        }
      }
    }
  }
}
