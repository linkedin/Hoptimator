plugins {
  id("com.github.spotbugs") version "6.1.10"
}

subprojects {
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.spotbugs'

  tasks.withType(JavaCompile).configureEach {
    options.release = 11
    options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-Xlint:unchecked'
  }
  tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  spotbugs {
    ignoreFailures = false
    showProgress = true
    reportsDir = file("$rootProject.layout.buildDirectory/spotbugs")
    includeFilter = file("$rootDir/config/spotbugs/include.xml")
    excludeFilter = file("$rootDir/config/spotbugs/exclude.xml")
  }

  plugins.withType(JavaPlugin) {
    dependencies {
      testImplementation libs.assertj
      testImplementation libs.junit.jupiter.api
      testImplementation libs.junit.jupiter.params
      testImplementation libs.junit.platform.launcher
      testImplementation libs.mockito.inline
      testImplementation libs.mockito.junit.jupiter
      testRuntimeOnly libs.junit.jupiter.engine
    }

    test {
      useJUnitPlatform {
        excludeTags 'integration'
      }
      testLogging {
        events "passed", "skipped", "failed"
      }
    }
  }
}
