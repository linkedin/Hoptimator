plugins {
  id 'java'
  id 'java-test-fixtures'
  id 'maven-publish'
}

dependencies {
  implementation project(':hoptimator-api')
  implementation project(':hoptimator-avro')
  implementation project(':hoptimator-util')
  implementation libs.avro
  implementation libs.calcite.core
  implementation libs.calcite.server
  implementation libs.slf4j.api

  testFixturesImplementation libs.quidem
  testFixturesImplementation libs.calcite.core
  testFixturesImplementation project(':hoptimator-api')
  testFixturesImplementation project(':hoptimator-util')
  testFixturesImplementation(platform('org.junit:junit-bom:5.11.3'))
  testFixturesImplementation 'org.junit.jupiter:junit-jupiter'

  testRuntimeOnly project(':hoptimator-demodb')
  testImplementation(platform('org.junit:junit-bom:5.11.3'))
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
  useJUnitPlatform {
    excludeTags 'integration'
  }
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.register('intTest', Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  shouldRunAfter test

  useJUnitPlatform {
    includeTags 'integration'
  }

  testLogging {
    events "passed", "skipped", "failed"
  }
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

// Work-around for gradle test fixtures bug, which spontaneously started publishing
// junit-bom to maven, for some reason.
components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) {
  skip()
}

components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) {
  skip()
}

publishing {
  repositories {
    maven {
      name 'GitHubPackages'
      url = 'https://maven.pkg.github.com/linkedin/Hoptimator'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name 'LinkedInJFrog'
      url 'https://linkedin.jfrog.io/artifactory/hoptimator'
      credentials {
        username = System.getenv('JFROG_USERNAME')
        password = System.getenv('JFROG_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = 'com.linkedin.hoptimator'
      artifactId = 'hoptimator-jdbc'
      version = System.getenv('VERSION')
      from components.java
      pom {
        name = 'LinkedIn Hoptimator'
        description = 'Multi-hop declarative data pipelines'
        url = 'https://github.com/linkedin/Hoptimator'
        licenses {
          license {
            name = 'BSD 2-Clause'
            url = 'https://raw.githubusercontent.com/linkedin/Hoptimator/main/LICENSE'
          }
        }
        scm {
          connection = 'scm:git:git://github.com:linkedin/Hoptimator.git'
          developerConnection = 'scm:git:ssh://github.com:linkedin/Hoptimator.git'
          url = 'https://github.com/linkedin/Hoptimator'
        }
      }
    }
  }
}
