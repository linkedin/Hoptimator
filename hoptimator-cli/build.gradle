plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
  id 'application'
  id 'idea'
  id 'maven-publish'
}

configurations {
  integration.extendsFrom implementation
}

dependencies {
  implementation project(':hoptimator-planner')
  implementation project(':hoptimator-catalog')

  // include adapters for integration tests
  integration project(':hoptimator-kafka-adapter')
  integration project(':hoptimator-mysql-adapter')
  integration libs.flinkCsv
  integration libs.flinkConnectorKafka
  integration libs.slf4jSimple

  implementation libs.avro 
  implementation libs.sqlline
  implementation libs.slf4jApi
  implementation libs.flinkClients
  implementation libs.flinkTableRuntime
  implementation libs.flinkTablePlanner
  implementation libs.calciteCore
  implementation libs.calciteAvatica

  testImplementation libs.junit
  testImplementation libs.assertj
}

publishing {
  repositories {
    maven {
      name 'GitHubPackages'
      url = 'https://maven.pkg.github.com/linkedin/Hoptimator'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name 'LinkedInJFrog'
      url 'https://linkedin.jfrog.io/artifactory/hoptimator'
      credentials {
        username = System.getenv('JFROG_USERNAME')
        password = System.getenv('JFROG_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = 'com.linkedin.hoptimator'
      artifactId = 'hoptimator-cli'
      version = System.getenv('VERSION')
      from components.java
      pom {
        name = 'LinkedIn Hoptimator'
        description = 'Multi-hop declarative data pipelines'
        url = 'https://github.com/linkedin/Hoptimator'
        licenses {
          license {
            name = 'BSD 2-Clause'
            url = 'https://raw.githubusercontent.com/linkedin/Hoptimator/main/LICENSE'
            }
          }
        scm {
          connection = 'scm:git:git://github.com:linkedin/Hoptimator.git'
          developerConnection = 'scm:git:ssh://github.com:linkedin/Hoptimator.git'
          url = 'https://github.com/linkedin/Hoptimator'
        }
      }

    }
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

shadowJar {
  configurations = [project.configurations.integration]

  // This is required for Flink and Avatica to play nicely
  relocate 'com.google', 'org.apache.flink.calcite.shaded.com.google'

  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
  zip64 true
  manifest.attributes 'Main-Class': 'com.linkedin.hoptimator.HoptimatorCliApp'
  mainClassName  = 'com.linkedin.hoptimator.HoptimatorCliApp'
  mergeServiceFiles()
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}
