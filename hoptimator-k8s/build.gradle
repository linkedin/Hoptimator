plugins {
  id 'java'
  id 'java-test-fixtures'
  id 'maven-publish'
}

dependencies {
  implementation project(':hoptimator-api')
  implementation project(':hoptimator-jdbc')
  implementation project(':hoptimator-util')
  implementation libs.calcite.server
  implementation libs.kubernetes.client

  testFixturesImplementation libs.kubernetes.client
  testFixturesImplementation project(':hoptimator-api')
  testFixturesImplementation project(':hoptimator-util')

  // These are included in case the respective Databases are installed
  testRuntimeOnly project(':hoptimator-demodb')
  testRuntimeOnly project(':hoptimator-kafka')
  testRuntimeOnly project(':hoptimator-venice')

  testImplementation(testFixtures(project(':hoptimator-jdbc')))
  testRuntimeOnly libs.slf4j.simple
}

tasks.register('intTest', Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  shouldRunAfter test

  useJUnitPlatform {
    includeTags 'integration'
  }

  testLogging {
    events "passed", "skipped", "failed"
  }
}

publishing {
  repositories {
    maven {
      name 'GitHubPackages'
      url = 'https://maven.pkg.github.com/linkedin/Hoptimator'
      credentials {
        username = System.getenv('GITHUB_ACTOR')
        password = System.getenv('GITHUB_TOKEN')
      }
    }
    maven {
      name 'LinkedInJFrog'
      url 'https://linkedin.jfrog.io/artifactory/hoptimator'
      credentials {
        username = System.getenv('JFROG_USERNAME')
        password = System.getenv('JFROG_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = 'com.linkedin.hoptimator'
      artifactId = 'hoptimator-k8s'
      version = System.getenv('VERSION')
      from components.java
      pom {
        name = 'LinkedIn Hoptimator'
        description = 'Multi-hop declarative data pipelines'
        url = 'https://github.com/linkedin/Hoptimator'
        licenses {
          license {
            name = 'BSD 2-Clause'
            url = 'https://raw.githubusercontent.com/linkedin/Hoptimator/main/LICENSE'
          }
        }
        scm {
          connection = 'scm:git:git://github.com:linkedin/Hoptimator.git'
          developerConnection = 'scm:git:ssh://github.com:linkedin/Hoptimator.git'
          url = 'https://github.com/linkedin/Hoptimator'
        }
      }
    }
  }
}
